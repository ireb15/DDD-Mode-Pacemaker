//VVI Pacemaker SCCharts
scchart Mode1 {
  input signal VSense, ASense;
  output signal VPace, APace;

  initial state top {
    signal LRI_start, LRI_ex, LRI_stop;
    signal URI_start, URI_ex, URI_stop;
    signal VRP_start, VRP_stop, VRP_ex;
    signal AVI_start, AVI_stop, AVI_ex;
    signal AEI_start, AEI_stop, AEI_ex;
    signal PVARP_start, PVARP_stop, PVARP_ex;
    
    int AVI_active;
    int AEI_active;
    int VRP_active;
    int PVARP_active;
    int LRI_active;
    int URI_active;
    

    region LRI:

    initial state init
    --> waitVRP with VSense / LRI_start //connector state commit1 --> waitVRP immediate with /LRI_start;
    --> waitVRP with VPace / LRI_start;

    state waitVRP
    --> waitLRI with VRP_ex;

    state waitLRI
    --> reInit with VSense / LRI_stop
    --> reInit with LRI_ex / VPace;

    state reInit
    --> waitVRP with / LRI_start;

    region URI:

    initial state init
    --> waitURI with VSense / URI_start //connector state commit1 --> waitVRP immediate with /LRI_start;
    --> waitURI with VPace / URI_start;

    state waitURI
    --> init with URI_ex / URI_stop;

    region LRI_timer:

    initial state idle
    --> timer with LRI_start / LRI_active = 1;

    state timer "timer"
    --> idle with LRI_stop / LRI_active = 0
    --> idle with LRI_ex / LRI_active = 0;

    region URI_timer:

    initial state idle
    --> timer with URI_start / URI_active = 1;

    state timer "timer"
    --> idle with URI_stop / URI_active = 0
    --> idle with URI_ex / URI_active = 0;

    region VRP:

    initial state idle
    --> comit1 with VSense
    --> comit1 with VPace;

    connector state comit1
    --> waitVRP immediate with / VRP_start;

    state waitVRP
    --> idle with VRP_ex
    --> comit2 with VSense;

    connector state comit2
    --> waitVRP immediate;

    region VRP_timer:

    initial state idle
    --> timer with VRP_start / VRP_active = 1;

    state timer "timer"
    --> idle with VRP_ex / VRP_active = 0;

    region AVI:

    initial state idle
    --> waitA with PVARP_ex;

    state waitA
    --> waitAVI with ASense / AVI_start
    --> waitAVI with APace / AVI_start;

    state waitAVI
    --> idle with VSense / AVI_stop
    --> extend with AVI_ex;

    state extend
    --> idle with URI_ex / VPace;

    region AVI_timer:

    initial state idle
    --> timer with AVI_start / AVI_active = 1;

    state timer "timer"
    --> idle with AVI_ex / AVI_active = 0;

    region AEI: //Needs to wait PVARP before WaitAEI


    initial state idle
    --> waitPVARP with VSense / AEI_start
    --> waitPVARP with VPace / AEI_start;

    state waitPVARP
    --> waitAEI with PVARP_ex;

    state waitAEI
    --> idle with ASense / AEI_stop
    --> idle with AEI_ex / APace;

    region AEI_timer:

    initial state idle
    --> timer with AEI_start / AEI_active = 1;

    state timer "timer"
    --> idle with AEI_ex / AEI_active = 0;

    region PVARP:

    initial state idle
    --> waitV with VRP_ex;

    state waitV
    --> waitPVARP with VSense / PVARP_start
    --> waitPVARP with VPace / PVARP_start;

    state waitPVARP
    --> idle with PVARP_ex;

    region PVARP_timer:

    initial state idle
    --> timer with PVARP_start / PVARP_active = 1;

    state timer "timer"
    --> idle with PVARP_ex / PVARP_active = 0;
  };
}